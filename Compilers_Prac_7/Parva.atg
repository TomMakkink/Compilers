using Library;
using System.Collections.Generic;

COMPILER Parva $NC
/* Parva level 1 grammar  - Coco/R for C#
   P.D. Terry, Rhodes University, 2016
   Java/C# operator precedences with short circuit evaluation
   Void functions, value parameter passing,
   but no globals or value returning functions
   int, bool, char types supported
   as for Prac 7, CSC 301 2018
   2018/09/04 */

  public static bool  // set/used by pragmas or cmd line args
    debug    = false,
    optimize = false,
    listCode = false,
    warnings = true,
    copy = false;
  const bool
    known = true;

  // This next method might better be located in the code
  // generator.  Traditionally it has been left in the ATG
  // file, but that might change in future years.
  //
  // Note that while sequences like \n \r and \t result in
  // special mappings to lf, cr and tab, other sequences  
  // like \x \: and \9 simply map to x, ; and 9 .  
  // Most students don't seem to know this!

  static string Unescape(string s) {
  /* Replaces escape sequences in s by their Unicode values */
    StringBuilder buf = new StringBuilder();
    int i = 0;
    while (i < s.Length) {
      if (s[i] == '\\') {
        switch (s[i+1]) {
          case '\\': buf.Append('\\'); break;
          case '\'': buf.Append('\''); break;
          case '\"': buf.Append('\"'); break;
          case  'r': buf.Append('\r'); break;
          case  'n': buf.Append('\n'); break;
          case  't': buf.Append('\t'); break;
          case  'b': buf.Append('\b'); break;
          case  'f': buf.Append('\f'); break;
          default:   buf.Append(s[i+1]); break;
        }
        i += 2;
      }
      else {
        buf.Append(s[i]);
        i++;
      }
    }
    return buf.ToString();
  } // Unescape

  // the following is global for expediency (fewer parameters needed)

  static Label mainEntryPoint = new Label(!known);

  static bool IsArith(int type) {
    return type == Types.intType || type == Types.charType || type == Types.noType;
  } // IsArith

  static bool IsBool(int type) {
    return type == Types.boolType || type == Types.noType;
  } // IsBool

  static bool IsArray(int type) {
    return (type % 2) == 1;
  } // IsArray

  static bool Compatible(int typeOne, int typeTwo) {
  // Returns true if typeOne is compatible (and comparable for equality) with typeTwo
    return    typeOne == typeTwo
           || IsArith(typeOne) && IsArith(typeTwo)
           || typeOne == Types.noType || typeTwo == Types.noType
           || IsArray(typeOne) && typeTwo == Types.nullType
           || IsArray(typeTwo) && typeOne == Types.nullType;
  } // Compatible

  static bool Assignable(int typeOne, int typeTwo) {
  // Returns true if a variable of typeOne may be assigned a value of typeTwo
    return    typeOne == typeTwo
           || typeOne == Types.intType && typeTwo == Types.charType
           || typeOne == Types.noType || typeTwo == Types.noType
           || IsArray(typeOne) && typeTwo == Types.nullType;
  } // Assignable

  static bool IsCall(out DesType des) {
    Entry entry = Table.Find(la.val);
    des = new DesType(entry);
    return entry.kind == Kinds.Fun;
  } // IsCall

/* -------------------------------------------------------- */

CHARACTERS
  lf         = CHR(10) .
  backslash  = CHR(92) .
  control    = CHR(0) .. CHR(31) .
  letter     = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz" .
  digit      = "0123456789" .
  stringCh   = ANY - '"' - control - backslash .
  charCh     = ANY - "'" - control - backslash .
  printable  = ANY - control .

TOKENS
  identifier = letter { { "_" } (letter | digit) } .
  number     = digit { digit } .
  stringLit  = '"' { stringCh | backslash printable } '"' .
  charLit    = "'" ( charCh   | backslash printable ) "'" .
  eol        = lf .

PRAGMAS
  CodeOn      = "$C+" .              (. listCode  = true; .)
  CodeOff     = "$C-" .              (. listCode  = false; .)
  DebugOn     = "$D+" .              (. debug     = true; .)
  DebugOff    = "$D-" .              (. debug     = false; .)
  StackDump   = "$SD" .              (. CodeGen.Stack(); .)
  HeapDump    = "$HD" .              (. CodeGen.Heap(); .)
  TableDump   = "$ST" . (. Table.PrintTable(OutFile.StdOut); .)

COMMENTS FROM "//" TO lf
COMMENTS FROM "/*" TO "*/"

IGNORE CHR(9) .. CHR(13)

PRODUCTIONS

  Parva
                             (. CodeGen.FrameHeader();
					     // no arguments
                                CodeGen.Call(mainEntryPoint);
						// forward, incomplete
                                CodeGen.LeaveProgram(); .)     						// return to O/S
  = {   FuncDeclaration } EOF  (. if 
						(!mainEntryPoint.IsDefined())
                         SemError("missing Main function"); .)
  .

  FuncDeclaration
                       (. StackFrame frame = new StackFrame();
                          Entry function = new Entry(); .)
  =  "void" Ident<out function.name>  
					(. function.kind = Kinds.Fun;
                              function.type = Types.voidType;
                              function.nParams = 0;
                              function.firstParam = null;
	                  function.entryPoint = new Label(known);
                              Table.Insert(function);
                              Table.OpenScope(); .)
     "(" FormalParameters<function> ")"      
				(. frame.size = CodeGen.headerSize + 
					function.nParams;
                   if (function.name.ToUpper().Equals("MAIN")
                       && !mainEntryPoint.IsDefined()
                       && function.nParams == 0) {
                           mainEntryPoint.Here(); } .)
     Body<frame>              (. Table.CloseScope(); .)
  .

  FormalParameters<Entry func>  (. Entry param; .)
  = [ OneParam<out param, func> (. func.firstParam = param; .)
      { WEAK "," OneParam<out param, func> }
    ] .

  OneParam<out Entry param, Entry func>
					(. param = new Entry();
                             param.kind = Kinds.Var;
           param.offset = CodeGen.headerSize + func.nParams;
                             func.nParams++; .)
  =  Type<out param.type>
     Ident<out param.name>         (. Table.Insert(param); .)
  .

  Body<StackFrame frame> (. Label DSPLabel = new Label(known);
                            int sizeMark = frame.size;
                            CodeGen.OpenStackFrame(0); .)
  =  "{" { Statement<frame,new Label(!known)> } WEAK "}"       
			(. CodeGen.FixDSP(DSPLabel.Address(), 
				frame.size - sizeMark);
                     CodeGen.LeaveVoidFunction(); .)
  .

  Statement<StackFrame frame, Label branch>        (. bool isConst = false; .) 
  =  SYNC (   Block<frame, branch>
            | ConstDeclarations
            | FinalDeclarations<frame>
            | VarDeclarations<frame, isConst>
            | AssignmentOrCall
            | IfStatement<frame, branch>
            | WhileStatement<frame>
            | HaltStatement
            | ReturnStatement
            | ReadStatement
            | WriteStatement
            | SwitchStatement<frame>
            | RepeatStatement<frame>
            | BreakStatement<branch>
            | DoWhileStatement<frame>
            | ";"
          ) .
    
  IncrDecreStatement<out bool inc>    (. inc = false; .)         /* Need to declare out value. */                                   
  = "++"                              (. inc = true;  .)
    | "--"                            (. inc = false; .)
                        
  .

  BreakStatement<Label branch>
  = "break" WEAK ";"              (. CodeGen.Branch(branch); .) /* The branch in codegen requires a label and uses that to then get the address to jump to.*/
  .

  Block<StackFrame frame, Label branch>
  =                                  (. Table.OpenScope(); .)
     "{" { Statement<frame, branch>  }
     WEAK "}"                        (. Table.CloseScope(); .)
  .

  FinalDeclarations<StackFrame frame>       (. int value; 
                                               bool isFinal = true; 
                                            .)
  = "final" VarDeclarations<frame, isFinal>
  .

  ConstDeclarations
  = "const" OneConst { WEAK "," OneConst }
    WEAK ";"
  .

  OneConst                   (. Entry constant = new Entry();
                                ConstRec con; .)
  =  Ident<out constant.name> (. constant.kind = Kinds.Con; .)
     AssignOp Constant<out con>  
			            (. constant.value = con.value;
                               constant.type = con.type;
                               Table.Insert(constant); .)
  .

  Constant<out ConstRec con>       (. con = new ConstRec(); .)
  =   IntConst<out con.value>  (. con.type = Types.intType; .)
    | CharConst<out con.value>(. con.type = Types.charType; .)
    | "true"                     (. con.type = Types.boolType; 
						    con.value = 1; .)
    | "false"                    (. con.type = Types.boolType;
						    con.value = 0; .)
    | "null"                     (. con.type = Types.nullType;
						    con.value = 0; .)
  .

  

  VarDeclarations<StackFrame frame, bool isConst>          (. int type; .)
  =  Type<out type>
     VarList<frame, type, isConst>
     WEAK ";"
  .

  Type<out int type>
  =  BasicType<out type>
     [ "[]"                                  (. type++; .)
     ] .


  BasicType<out int type>         (. type = Types.noType; .)
  =   "int"                       (. type = Types.intType; .)
    | "bool"                      (. type = Types.boolType; .)
    | "char"                      (. type = Types.charType; .)
  .


  VarList<StackFrame frame, int type, bool isConst>
  =  OneVar<frame, type, isConst>
     { WEAK "," OneVar<frame, type, isConst> }
  .

  OneVar<StackFrame frame, int type, bool isConst>    
                                    (. int expType;
                                   Entry var = new Entry(); .)
  =  Ident<out var.name>           (. if (isConst) var.final = 1; 
                                      var.kind = Kinds.Var;
                                      var.type = type;
                                      var.offset = frame.size;
                                      frame.size++; .)
     [ AssignOp                (. CodeGen.LoadAddress(var); .)
       Expression<out expType>   (. if (!Assignable(var.type,
							 expType))
                 SemError("incompatible types in assignment");
                                  CodeGen.Assign(var.type); 
                                  .)
     ]                                (. Table.Insert(var); .)
  .


  AssignmentOrCall                     (. int expType; DesType des; bool inc; .)
  =  (  IF (IsCall(out des)) // resolver handles LL1 conflict
         identifier               (. CodeGen.FrameHeader(); .)
         "(" Arguments<des> ")"   (. CodeGen.Call
						    (des.entry.entryPoint); .)
      |  Designator<out des>      (. if (des.entry.kind != Kinds.Var || des.entry.final == 1)
                         		            SemError("cannot assign to " + Kinds.kindNames[des.entry.kind]); .)
        ((
         AssignOp
         Expression<out expType>  (. if (!Assignable(des.type,expType))
                                SemError("incompatible types in assignment");
                                if (!copy) CodeGen.Assign(des.type);
                                copy = false; 
                                if (des.entry.final == 2) des.entry.final = 1; 
                                .)
        )
     | IncrDecreStatement<out inc> WEAK ";" (. if (!IsArith(des.type) || des.entry.final == 1) SemError("Incompatiable type."); 
                                               else CodeGen.IncOrDec(inc, des.type); .))
     | IncrDecreStatement<out inc>                                      
          Designator<out des>               (. if (des.entry.kind != Kinds.Var || des.entry.final == 1) SemError("cannot assign to " + Kinds.kindNames[des.entry.kind]);
                                               if (!IsArith(des.type)) SemError("Incompatiable type."); 
                                               else CodeGen.IncOrDec(inc, des.type);  
                                            .)
     )
  .

  Arguments<DesType des>        (. int argCount = 0;
                           Entry fp = des.entry.firstParam; .)
  =  [ OneArg<fp>               (. argCount++; if (fp != null)
							 fp = fp.nextInScope; .)
       { WEAK "," OneArg<fp>    (. argCount++; if (fp != null)
							 fp = fp.nextInScope;.)
       }
     ]                          (. if (argCount !=
								 des.entry.nParams)
                     SemError("wrong number of arguments"); .)
  .

  OneArg<Entry fp>                 (. int argType; .)
  =  (  Expression<out argType>    (. if (fp != null && 
						!Assignable(fp.type, argType))
                        SemError("argument type mismatch"); .)
     )
  .

  Designator<out DesType des>           (. string name; 
                                           int indexType; 
                                            
                                        .)
  = Ident<out name>        (. Entry entry = Table.Find(name);
                               if (!entry.declared)
                           SemError("undeclared identifier");
                               des = new DesType(entry);
                               if (entry.kind == Kinds.Var)
                                CodeGen.LoadAddress(entry); 
                              if (des.entry.final == 2) des.entry.final = 1;  
                                .)
     [  "["              	 (. if (IsArray(des.type)) des.type--; else SemError("unexpected subscript");
                              if (des.entry.kind != Kinds.Var) SemError("unexpected subscript"); 
                              CodeGen.Dereference(); .)
        Expression<out indexType>  (. if (!IsArith(indexType)) SemError("invalid subscript type"); CodeGen.Index(); 
                                      des.entry.final = 2; .)
           "]"
     ]
  .

  IfStatement<StackFrame frame, Label loopExit>
                   			          (. Label falseLabel = new Label(!known);
                                     Label trueLabel  = new Label(!known); .)
  =  "if" "(" Condition ")"       (. CodeGen.BranchFalse(falseLabel); .)
      [ "then"                    (. SemError("'then' invalid."); .)  /* Perhaps just throw a warning here rather. */
      ]
        Statement<frame,loopExit>          (. CodeGen.Branch(trueLabel); 
                                     falseLabel.Here(); .)
    { "elsif" "(" Condition ")"   (. Label elsIfLabel = new Label(!known);
                                     CodeGen.BranchFalse(elsIfLabel); .)
        Statement<frame,loopExit>          (. CodeGen.Branch(trueLabel); 
                                     elsIfLabel.Here(); .)
    }
    [ "else" Statement<frame, loopExit>  ]   (. trueLabel.Here(); .) 
                             
  .


  WhileStatement<StackFrame frame>    (. Label loopExit  = new Label(!known);
                                         Label loopStart = new Label(known); .)

  =  "while" "(" Condition ")"        (. CodeGen.BranchFalse(loopExit); .)

     Statement<frame, loopExit>              (. CodeGen.Branch(loopStart);
                                                loopExit.Here(); .)
  .

  RepeatStatement<StackFrame frame>    (. Label loopStart = new Label(known); 
                                         .)
 = "repeat" {Statement<frame, new Label(!known)>}         
   "until" "(" Condition ")" WEAK ";"  (. CodeGen.BranchFalse(loopStart); .)
   . 
                                          
  DoWhileStatement<StackFrame frame>   (. Label loopStart = new Label(known); .) 
  = "do" Statement<frame,  new Label(!known) > 
   "while" "(" Condition ")" WEAK ";"  (. CodeGen.NegateBoolean(); 
                                          CodeGen.BranchFalse(loopStart); .) 
   .


  SwitchStatement<StackFrame frame>       (. int expType; 
                                             Label exitSwitch = new Label(!known); 
                                          .)
  = "switch"
    "(" Expression<out expType>           (. if (!IsArith(expType)) SemError(
                                             "Invalid condition type.");  .)
    ")" "{"
      {                                   (. CodeGen.Duplicate();.)
        CaseLabelList                     (. Label repeatSwitch = new Label(!known);  
                                             CodeGen.Comparison(CodeGen.ceq, expType); 
                                             CodeGen.BranchFalse(repeatSwitch);.)
        Statement<frame, exitSwitch> 
        { Statement<frame,  exitSwitch>
        } 
                                          (. CodeGen.Branch(exitSwitch);
                                             repeatSwitch.Here(); 
                                             CodeGen.Duplicate();
                                          .)  
        }
      [ "default" ":"
        { Statement<frame,  exitSwitch>
        } ]
                                          (. exitSwitch.Here(); 
                                             CodeGen.RemoveDuplicate(); .) 
    "}"                                 
  .

  CaseLabelList
  = CaseLabel { CaseLabel }
  .

  CaseLabel                              	(. ConstRec con; bool isNeg = false; .)
  = "case" [ "+" | "-"                      (. isNeg = true; .)
  ]
    Constant<out con>                       (. if (isNeg) con.value = con.value * -1;  
                                            CodeGen.LoadConstant(con.value); .)
    ":"
  .

  HaltStatement        
  =  "halt"   
  ["(" WriteList ")" ]
                                            (. CodeGen.LeaveProgram(); .)
     WEAK ";"
  .

  ReturnStatement
  =  "return"               (. CodeGen.LeaveVoidFunction(); .)
     WEAK ";"
  .

  ReadStatement
  = "read" "(" ReadList ")" WEAK ";"
  | "readLine" "(" [ReadList] ")" WEAK ";"           (. CodeGen.ReadLine(); .) 
  .

  ReadList
  = ReadElement { WEAK "," ReadElement }
  .

  ReadElement                        (. string str;
                                        DesType des; .)
  =   StringConst<out str>     (. CodeGen.WriteString(str); .)
    | Designator<out des>            (. if (des.entry.kind !=
								 Kinds.Var || des.entry.final == 1)
                         SemError("wrong kind of identifier");
                                  switch (des.type) {
                                    case Types.intType:
                                    case Types.boolType:
                                    case Types.charType:
                                       CodeGen.Read(des.type);
							  break;
                                    default:
                            SemError("cannot read this type");
							  break;
                                                    } .)
  .

  WriteStatement
  =  "write" "(" WriteList ")" WEAK ";"
  | "writeLine" "(" [WriteList] ")" WEAK ";"  (. CodeGen.WriteString("\n");.)
  .

  WriteList                            
  = WriteElement{ WEAK "," WriteElement }
  .

  WriteElement (. int expType; string str;.)
  =   StringConst<out str>     
    ( ":" Expression<out expType>              (. if (!IsArith(expType)) SemError("Write must be formatted with a numeric type"); 
                                                  CodeGen.WriteFormatString(str); .)
    |                                          (. CodeGen.WriteString(str); .)
    )
    | Expression<out expType>                  (. if (!(IsArith(expType) || expType == Types.boolType))
                                                        SemError("cannot write this type");.)
    ( ":" Expression<out expType>               (. if (!IsArith(expType)) SemError("Write must be formatted with a numeric type"); 
                                                   CodeGen.WriteFormat(); .)
    |                                           (. switch (expType) {
                                                    case Types.intType:
                                                    case Types.boolType:
                                                    case Types.charType:
                                                        CodeGen.Write(expType);
							                        break;
                                                    default:  break;
                                                    } .)
    )
  .

  Condition                       (. int type; .)
  =  Expression<out type>         (. if (!IsBool(type))
                     SemError("Boolean expression needed");.)
  .

  Expression<out int type>        (. int type2;
                    Label shortcircuit = new Label(!known); .)
  =  AndExp<out type>
     { "||"                       (. CodeGen.BooleanOp
						(shortcircuit, CodeGen.or); .)
       AndExp<out type2>          (. if (!IsBool(type) || 
								!IsBool(type2))
                          SemError("Boolean operands needed");
                                     type = Types.boolType; .)
     }                            (. shortcircuit.Here(); .)
  .

  AndExp<out int type>            (. int type2;
                                     Label shortcircuit = new
								 Label(!known); .)
  =  EqlExp<out type>
     { "&&"                       (. CodeGen.BooleanOp
					(shortcircuit, CodeGen.and); .)
       EqlExp<out type2>          (. if (!IsBool(type) || 
							!IsBool(type2))
                       SemError("Boolean operands needed");
                                     type = Types.boolType; .)
     }                            (. shortcircuit.Here(); .)
  .

  EqlExp<out int type>            (. int type2;
                                     int op; .)
  =  RelExp<out type>
     { EqualOp<out op>
       RelExp<out type2>      (. if (!Compatible(type, type2))
                       SemError("incomparable operand types");
                                 CodeGen.Comparison(op, type);
                                 type = Types.boolType; .)
     }
  .

  RelExp<out int type>           (. int type2; int op; .)
  =  AddExp<out type>
     [ RelOp<out op>
       AddExp<out type2>         (. if (!IsArith(type) || 
								!IsArith(type2))
                    SemError("incomparable operand types");
                                 CodeGen.Comparison(op, type);
                                 type = Types.boolType; .)
     ]
  .

  AddExp<out int type>           (. int type2; int op; .)
  =  MultExp<out type>
     { AddOp<out op>
       MultExp<out type2>        (. if (IsArith(type) &&
							 IsArith(type2)) {
                                      type = Types.intType;
                                      CodeGen.BinaryOp(op);
                                     }
                                    else {
                       SemError("arithmetic operands needed");
                                          type = Types.noType;
                                         } .)
     }
  .

  MultExp<out int type>           (. int type2; int op; .)
  =  Factor<out type>
     { MulOp<out op>
       Factor<out type2>          (. if (IsArith(type) &&
							   IsArith(type2)) {
                                       type = Types.intType;
                                       CodeGen.BinaryOp(op);
                                       }
                                      else {
                       SemError("arithmetic operands needed");
                                          type = Types.noType;
                                           } .)
     }
  .

  Factor<out int type>              (. type = Types.noType; .)
  =    Primary<out type>
     | "+" Factor<out type>         (. if (!IsArith(type)) {
                        SemError("arithmetic operand needed");
                                          type = Types.noType;
                                        }
                                       else 
							type = Types.intType; .)
     | "-" Factor<out type>         (. if (!IsArith(type)) {
                        SemError("arithmetic operand needed");
                                          type = Types.noType;
                                        }
                                       else
							   type = Types.intType;
                                    CodeGen.NegateInteger();.)
     | "!" Factor<out type>     (. if (!IsBool(type))
                           SemError("Boolean operand needed");
                                   type = Types.boolType;
                                   CodeGen.NegateBoolean(); .)
  .

  Primary<out int type>         (. type = Types.noType;
                                   int size;
                                   DesType des;
                                   ConstRec con; .)
  =    Designator<out des>      (. type = des.type;
                                   switch (des.entry.kind) {
                                    case Kinds.Var:
                                        CodeGen.Dereference();
                                        break;
                                    case Kinds.Con:
                        CodeGen.LoadConstant(des.entry.value);
                                        break;
                                    default:
                         SemError("wrong kind of identifier");
                                        break;
                                    } .)
     | Constant<out con>        (. type = con.type;
                            CodeGen.LoadConstant(con.value);.)
     | "new"
       (   BasicType<out type>
           "[" Expression<out size>    (. if (!IsArith(size))
                       SemError("array size must be integer");
                                          type++; .)
           "]"
       )                             (. CodeGen.Allocate(); .)

     | "("
       (   "char" ")"
           Factor<out type>     	     (. if (!IsArith(type))
                                     SemError("invalid cast");
                                       else 
							  type = Types.charType;
                                      CodeGen.CastToChar(); .)
         | "int" ")"
           Factor<out type>          (. if (!IsArith(type))
                                     SemError("invalid cast");
                                       else 
							type = Types.intType; .)
         | Expression<out type> ")"
       )
     | "Max" "("                    (. int maxCount = 0; .)
     Expression<out type>           (. if(!IsArith(type)) SemError("Must be int.");
                                        maxCount++; .)
     { "," Expression<out type>     (. if (!IsArith(type)) SemError("Must be int");
                                       maxCount++; .) 
     }                              (. CodeGen.LoadConstant(maxCount); 
                                       CodeGen.Max(); .)                                            
     ")"
     | "Min" "("                    (. int minCount = 0; .) 
     
     Expression<out type>          (. if(!IsArith(type)) SemError("Must be int."); 
                                    minCount++; .) 
     { "," Expression<out type>    (. if(!IsArith(type)) SemError("Must be int."); 
                                   minCount++; .) 
     }
                                   (. CodeGen.LoadConstant(minCount); 
                                      CodeGen.Min(); .)
    ")"
    | "Sqr" "("                    
    Expression<out type>           (. if (!IsArith(type)) SemError("Must be int"); .) 
    ")"                            (. CodeGen.Sqr();.)
    | "Length" "("                           (. int ArrType; .)
              Expression<out ArrType> ")"   (.   if(!IsArray(ArrType)) SemError("Incompatable type. Please use an array.");
                                                  else{ ArrType = Types.intType;
                                                        type = ArrType;
                                                        CodeGen.Dereference();                 
                                                        }.) 
    | "Copy" "("                           
          Designator<out des>      (. if (!IsArray(des.type)) SemError("Incompatable type. Please use an array.");
                                            else CodeGen.Copy();
                                            copy = true;
                                            type = des.type; .)
               ")" 
    | "Equals" "("
          Designator<out des>            (. if(!IsArray(des.type)) SemError("Incompatable type. Please use an array.");
                                         .)
          "," Designator<out des>         (. if(!IsArray(des.type)) SemError("Incompatable type. Please use an array.");
                                             CodeGen.Equals();
                                             type = Types.boolType;
                                           .)
    ")"
  .

  AddOp<out int op>                   (. op = CodeGen.nop; .)
  =    "+"                            (. op = CodeGen.add; .)
     | "-"                            (. op = CodeGen.sub; .)
  .

  MulOp<out int op>                   (. op = CodeGen.nop; .)
  =    "*"                            (. op = CodeGen.mul; .)
     | "/"                            (. op = CodeGen.div; .)
     | "%"                            (. op = CodeGen.rem; .)
  .

  EqualOp<out int op>                 (. op = CodeGen.nop; .)
  =    "=="                           (. op = CodeGen.ceq; .)
     | "!="                           (. op = CodeGen.cne; .)
  .

  RelOp<out int op>                   (. op = CodeGen.nop; .)
  =    "<"                            (. op = CodeGen.clt; .)
     | "<="                           (. op = CodeGen.cle; .)
     | ">"                            (. op = CodeGen.cgt; .)
     | ">="                           (. op = CodeGen.cge; .)
  .

  AssignOp = "=" .

  Ident<out string name>
  =  identifier                       (. name = token.val; .)
  .

  StringConst<out string str>
  =  stringLit                        (. str = token.val;
                                         str = Unescape
				(str.Substring(1, str.Length - 2)); .)
  .

  CharConst<out int value>
  =  charLit                        (. string str = token.val;
                                       str = Unescape
					(str.Substring(1, str.Length - 2));
                                       value = str[0]; .)
  .

  IntConst<out int value>
  =  number                         (. try {value =
						 Convert.ToInt32(token.val);
                                       } catch (Exception) {
                                         value = 0; 
					SemError("number out of range");
                                             } .)
  .

END Parva.

